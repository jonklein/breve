#
# GrayScott -- the Gray Scott reaction diffusion model
# jon klein <jk@spiderland.org>
#
# This is a simulation of the Gray Scott self-reproducing spots
# reaction/diffusion model.
#

@use Control.
@use Mobile.
@use Matrix.
@use PatchGrid.

@define F 0.01.
@define K 0.04.
@define RU .058.
@define RV .022.

@define TIMESTEP 1.0.

@define XSIZE 32.
@define YSIZE 32.
@define ZSIZE 16.

Controller GS.

Control : GS {
    + variables:
        U (object).
        V (object).
        tempreact, temp, inflow (object).

		grid (object).
        
    + to init:
        i, j, k, m, n (int).

        self set-background-color to (1, 1, 1).

        self set-iteration-step to TIMESTEP.
        self set-integration-step to TIMESTEP.

        self point-camera at (0, 0, 0) from (30, 6, -30).

		U = new Matrix3D.
		V = new Matrix3D.
		inflow = new Matrix3D.
		temp = new Matrix3D.
		tempreact = new Matrix3D.

		U set-size x XSIZE y YSIZE z ZSIZE.
		V set-size x XSIZE y YSIZE z ZSIZE.
		temp set-size x XSIZE y YSIZE z ZSIZE.
		tempreact set-size x XSIZE y YSIZE z ZSIZE.
		inflow set-size x XSIZE y YSIZE z ZSIZE.

		grid = new PatchGrid.
		grid init-with patch-size (.5, .5, .5) x-count XSIZE y-count YSIZE z-count ZSIZE.

		grid enable-smooth-drawing.

		inflow set-all-values to F.

        for i=0,i<ZSIZE,i++:
            for n=0,n<YSIZE,n++:
                for m=0,m<XSIZE,m++: {
                    U set-value to (0.5 + sqrt(|0.25 - F*(1.0 + K/F)*(1.0 + K/F)|) + 0.01*(random[1.0] - 0.5)) at-x m at-y n at-z i.
                    V set-value to (1.0 - (U get-value at-x m at-y n at-z i))/(1.0 + K/F) + 0.01*(random[1.0] - 0.5) at-x m at-y n at-z i.
                }

		all Patches set-transparency to 0.9.
        
    + to iterate:
		tempreact copy from U.
		tempreact multiply-with-values of V.
		tempreact multiply-with-values of V.

		temp compute-periodic-diffusion-matrix from U with-scale RU.

		temp add-values of tempreact scaled-by -1.0.
		temp add-values of U scaled-by -F.
		temp add-values of inflow.
		U add-values of temp scaled-by TIMESTEP.

		temp compute-periodic-diffusion-matrix from V with-scale RV.

		temp add-values of tempreact.
		temp add-values of V scaled-by -(K + F).
		V add-values of temp scaled-by TIMESTEP.

		V copy-to-red-channel of-patch-grid grid with-scale 2.0.

		temp copy from V.
		temp multiply-with-values of V.
		temp copy-to-alpha-channel of-patch-grid grid with-scale 0.5.

        super iterate.
}
